programa uziel4
procesos
  proceso juntarF(ES bolsa:numero)
  comenzar
    mientras(HayFlorEnLaEsquina)
      tomarFlor
      bolsa:=bolsa+1
  fin
  proceso recoger(E av1:numero E ca1:numero ES bolsa:numero)
  variables
    ca:numero
    av:numero
  comenzar
    av:=PosAv
    ca:=PosCa
    BloquearEsquina(av1,ca1)
    Pos(av1,ca1)
    juntarF(bolsa)
    Pos(av,ca)
    LiberarEsquina(av1,ca1)
  fin
  proceso soltarF(E bolsa:numero)
  comenzar
    repetir bolsa
      depositarFlor
  fin
  proceso dejarF(E bolsa:numero)
  variables
    av:numero
    ca:numero
  comenzar
    av:=PosAv
    ca:=PosCa
    BloquearEsquina(10,10)
    Pos(10,10)
    soltarF(bolsa)
    Pos(av,ca)
    LiberarEsquina(10,10)
  fin
areas
  areap1: AreaP(1,1,1,1)
  areap2: AreaP(2,1,2,1)
  areap3: AreaP(3,1,3,1)
  areaFisc: AreaP(4,1,4,1)
  areaComp: AreaPC(40,40,60,60)
  areaComp2: AreaPC(10,10,10,10)
robots
  robot florero
  variables
    ca:numero
    av:numero
    bolsa:numero
    nombre:numero
    cod:boolean
  comenzar
    RecibirMensaje(nombre,fiscal)
    RecibirMensaje(cod,fiscal)
    bolsa:=0
    mientras(cod=V)
      RecibirMensaje(av,fiscal)
      RecibirMensaje(ca,fiscal)
      recoger(av,ca,bolsa)
      RecibirMensaje(cod,fiscal)
    EnviarMensaje(bolsa,fiscal)
    dejarF(bolsa)
  fin
  robot fical
  variables
    av:numero
    ca:numero
    nombre:numero
    bolsaT:numero
    bolsa:numero
  comenzar
    EnviarMensaje(1,robot1)
    EnviarMensaje(2,robot2)
    EnviarMensaje(3,robot3)
    bolsaT:=0
    repetir 8
      Random(nombre,1,3)
      Random(av,40,60)
      Random(ca,40,60)
      si(nombre=1)
        EnviarMensaje(V,robot1)
        EnviarMensaje(av,robot1)
        EnviarMensaje(ca,robot1)
      sino
        si(nombre=2)
          EnviarMensaje(V,robot2)
          EnviarMensaje(av,robot2)
          EnviarMensaje(ca,robot2)
        sino
          EnviarMensaje(V,robot3)
          EnviarMensaje(av,robot3)
          EnviarMensaje(ca,robot3)
    EnviarMensaje(F,robot1)
    EnviarMensaje(F,robot2)
    EnviarMensaje(F,robot3)
    repetir 3
      RecibirMensaje(bolsa,*)
      bolsaT:=bolsaT+bolsa
    Informar(bolsaT)
  fin
variables
  robot1:florero
  robot2:florero
  robot3:florero
  fiscal:fical
comenzar
  AsignarArea(robot1,areap1)
  AsignarArea(robot1,areaComp)
  AsignarArea(robot1,areaComp2)
  AsignarArea(robot2,areap2)
  AsignarArea(robot2,areaComp)
  AsignarArea(robot2,areaComp2)
  AsignarArea(robot3,areap3)
  AsignarArea(robot3,areaComp)
  AsignarArea(robot3,areaComp2)
  AsignarArea(fiscal,areaFisc)
  Iniciar(robot1,1,1)
  Iniciar(robot2,2,1)
  Iniciar(robot3,3,1)
  Iniciar(fiscal,4,1)
fin